plugins {
    id 'java'
    id "com.github.node-gradle.node" version "5.0.0"
    id "io.freefair.lombok" version "9.2.0"
    id "run.halo.plugin.devtools" version "0.6.2"
}

group 'cc.ryanc.staticpages'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.22.0')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

test {
    useJUnitPlatform()
}

node {
    // Explicitly set Node.js and pnpm versions for reproducible builds
    // pnpm version must match packageManager in ui/package.json
    version = '20'
    pnpmVersion = '10.22.0'
    nodeProjectDir = file("${project.projectDir}/ui")
}

tasks.register('buildFrontend', PnpmTask) {
    args = ['build']
    dependsOn('installDepsForUI')
}

tasks.register('installDepsForUI', PnpmTask) {
    args = ['install']
}

build {
    // build frontend before build
    tasks.named('compileJava').configure {
        dependsOn('buildFrontend')
    }
}

halo {
    version = '2.22'
    debug = true
}

haloPlugin {
    watchDomains {
        consoleSource {
            files files('theme')
            exclude "**/src/main/resources/**"
        }
    }
    openApi {
        outputDir = file("$rootDir/api-docs/openapi/v3_0")
        groupingRules {
            staticPageApi {
                displayName = 'Extension API for Static Page'
                pathsToMatch = [
                    '/apis/staticpage.halo.run/v1alpha1/**',
                    '/apis/console.api.staticpage.halo.run/v1alpha1/**'
                ]
            }
        }
        groupedApiMappings = [
                '/v3/api-docs/staticPageApi': 'staticPageApi.json'
        ]
        generator {
            outputDir = file("${projectDir}/ui/src/api/generated")
        }
    }
}
